import org.ajoberstar.gradle.git.release.opinion.Strategies
import org.ajoberstar.gradle.git.release.semver.RebuildVersionStrategy

import br.lnls.dig.gradle.updatesemvermacrostask.UpdateSemVerMacrosTask

apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'org.ajoberstar.release-opinion'
apply plugin: br.lnls.dig.gradle.nativedistribution.plugins.NativeDistributionPlugin
apply plugin: 'br.lnls.dig.gradle.nativerelease'
apply plugin: com.janitovff.grgit.config.GitConfigPlugin

model {
    components {
        revision(NativeLibrarySpec) {
            sources {
                c {
                    exportedHeaders.srcDir 'include'

                    lib project: ':libs:convc', library: 'convc'
                    lib project: ':libs:errhand', library: 'errhand'
                    lib project: ':libs:hutils', library: 'hutils'
                }
            }

            binaries.all {
                cCompiler.define "GIT_REVISION=\"$project.version\""
                cCompiler.define "GIT_USER_NAME=\"$gitConfig.user.name\""
                cCompiler.define "GIT_USER_EMAIL=\"$gitConfig.user.email\""
            }
        }
    }
}

release {
    versionStrategy Strategies.FINAL.copyWith(
        name: 'ci_release',
        stages: ['ci_release'] as SortedSet,
        allowDirtyRepo: true
    )

    defaultVersionStrategy = RebuildVersionStrategy.INSTANCE
}

task updateVersionMacros(type: UpdateSemVerMacrosTask) {
    macroPrefix 'HALCS_VERSION_'
    file project.file('include/revision.h')
}
